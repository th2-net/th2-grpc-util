buildscript {
    repositories {
        mavenCentral()
        maven {
            name 'Schema snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/th2-schema-snapshot-local'
        }
        maven {
            name 'Artifactory snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }
        mavenLocal()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
        classpath "com.exactpro.th2:service-generator:1.3.1-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "com.google.protobuf"
apply plugin: "com.exactpro.th2.service-generator"
apply plugin: "maven-publish"


group = 'com.exactpro.grpc'
version = "1.0-SNAPSHOT"

sourceCompatibility = 1.11
targetCompatibility = 1.11

ext {
    grpcVersion         = '1.32.1'
    protobufVersion     = '3.13.0'

    genDir              = file('src/gen')
    sharedDir           = file("${project.rootDir}/shared")
}

repositories {
    mavenCentral()
    maven {
        name 'Schema snapshot'
        url 'http://artifactory5.exp.exactpro.com/artifactory/th2-schema-snapshot-local'
    }
    maven {
        name 'Artifactory snapshot'
        url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
    }
    mavenLocal()
}

dependencies {
    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-core:${grpcVersion}"
    compile "io.grpc:grpc-netty:${grpcVersion}"

    compile "javax.annotation:javax.annotation-api:1.3.2"

    compile "com.exactpro.th2:service-generator:1.3.1-SNAPSHOT"
}

sourceSets {
    main {
        proto {
            srcDirs 'src/main/proto'
        }
        java {
            srcDirs 'src/gen/main/java', 'src/gen/main/grpc', 'src/gen/main/services'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = genDir
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}

generateServices.dependsOn.add('generateProto')
compileJava.dependsOn.add('generateServices')

serviceGeneration {
    settings {
        protoDir = "src/main/proto"
        outDir = "src/gen/main/services"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "localRepo"
            url = sharedDir
        }
    }
}

clean {
    delete genDir
    delete sharedDir
}